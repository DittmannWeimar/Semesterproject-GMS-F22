grammar dk.gms.dsl.DeviceDefinition with org.eclipse.xtext.common.Terminals

generate deviceDefinition "http://www.gms.dk/dsl/DeviceDefinition"

System:
	'System' name=ID
	master=Master
	graphs += Graph*
;

Graph:
	'Graph' name=ID
	'type' type = GraphType
	'category' category = STRING
	'title' title = STRING
	'xlabel' xlabel = STRING
	'ylabel' ylabel = STRING
;

enum GraphType:
	Line | BoxPlot | Histogram
;
	
Master:
	'Master' name=ID
	'bluetooth' bluetoothName=STRING
	slaves += Slave+
;

Slave:
	'Slave' name=ID
	'bluetooth' bluetoothName=STRING
	sensors += Sensor*
	actuators += Actuator*
;

Sensor:
	'Sensor' type = SensorType
	'pins' '(' pins += INT* (',' pins += INT)* ')'
	'graph' graph = [Graph]
;

enum SensorType:
	Moisture | Temperature | HumidityAndTemperature | AirHumidity | CO2 | Light
;

Actuator:
	'Actuator' type = ActuatorType
	'pins' '(' pins += INT* (',' pins += INT)* ')'
	settings += Setting*
;

enum ActuatorType:
	LED | WaterPump
;

Setting:
	'Setting' name=ID
	'default' value=INT
;